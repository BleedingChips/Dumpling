^ := '\s+|/\*.*?\*/|//.*?\n'
Code := '@\{[.\n]*?\}@' : [3]
FloatNumber := '-?[0-9]+\.[0-9]+' : [0]
IntNumber := '-?[1-9][0-9]*' : [1]
String := '".*?[^\\]"|""' : [2]
Id := '[a-zA-Z_][a-zA-Z_0-9]*'

%%%

$ := <Statement>

<BaseConstValue> := IntNumber | FloatNumber | String : [4]
    := 'true' : [5]
    := 'false' : [6]
    := Id '(' [ <BaseConstValue> {',' <BaseConstValue> } ] ')' : [7]

<BaseConstValueList> := '{'[ <BaseConstValue> {',' <BaseConstValue> } ] '}' : [22]

<VariableTypeRequire> := Id [ '<' Id '>' ] : [20]

<VariableAttriableDefine> := <VariableTypeRequire> Id : [8]
    := <VariableAttriableDefine> '[' ']' : [10]
    := <VariableAttriableDefine> '[' IntNumber ']' : [9]

<DefineVariable> := <VariableAttriableDefine> [ '=' ( <BaseConstValue> | <BaseConstValueList> ) ] ';' : [11]

<StructDefineVariable> := Id Id : [23]

<StructDesc> := 'struct' Id '{' { <StructDefineVariable> ';' | ';' } '}' ';' : [12]

<MateDateElement> := Id [ '=' <BaseConstValue> ] : [13]

<MateDate> := [ '[' [<MateDateElement> {',' <MateDateElement> } ] ']' ] : [14]

<PropertyDefine> := <MateDate> '{' { <PropertyDefine> } '}' : [15]
    := <MateDate> <DefineVariable> : [16]

<PropertyDefineList> := : [17]
    := <PropertyDefineList> <PropertyDefine> : [18]
    := <PropertyDefineList> <StructDesc> : [19]

<PropertyStatement> := 'property' '{' <PropertyDefineList> '}' ';' : [21]

<ImportStatement> := 'import' String 'as' Id ';' : [30]

<ReferencePath> := Id : [31]
    := <ReferencePath> '.' Id : [32]

<References> := [ '[' [ <ReferencePath> { ',' <ReferencePath> } ] ']' ] : [33]

<CodeStatement> :=  <MateDate> 'code' Id <References> Code ';' : [34]

<SnippetParemeter> := [ 'in' | 'out' ] Id Id : [36]

<SnippetParemeterList> := '(' [<SnippetParemeter> {',' <SnippetParemeter> } ] ')' : [37]

<SnippetStatement> :=  <MateDate> 'snippet' Id <References> <SnippetParemeterList> Code ';' : [38]

<UsingSnippetStatement> := 'using' 'snippet' <ReferencePath> 'as' Id ';' : [39]

<DefineStatement> := 'define' Id String ';' : [40]

<MaterialElementList> := : [17]
    := <MaterialElementList> ( <SnippetStatement> | <CodeStatement> | <DefineVariable> | <UsingSnippetStatement> | <DefineStatement> ) : [19]

<MaterialStatement> := <MateDate> 'material' String 'as' Id '{' <MaterialElementList> '}' ';' : [42]

<StatementElement> := <PropertyStatement> : [1]
    := : [1]
    := <StatementElement> <ImportStatement> : [1]
    := <StatementElement> <CodeStatement> : [1]
    := <StatementElement> <MaterialStatement> : [1]
    := <StatementElement> <SnippetStatement> : [1]
    := <StatementElement> ';' : [1]

<Statement> := <StatementElement> : [1]
