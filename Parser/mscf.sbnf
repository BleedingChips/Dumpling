_IGNORE := '\s+'
Code := '@\{[.\n]*?\}@'
IntNumber := '-?[1-9][0-9]*'
FloatNumber := '-?[1-9][0-9]*(\.[0-9]+)?'
String := '".*?[^\\]"|""'
Id := '[a-zA-Z_][a-zA-Z_0-9]*'

%%%

<NumberType> := 'int' | 'int2' | 'int3' | 'int4' | 'float' | 'float2' | 'float3' | 'float4'

<TextureType> := 'Texture2D' | 'Texture1D'

<RWTextureType> := ( 'RWTexture1D' | 'RWTexture2D' ) '<' <NumberType> '>'

<Type> := <NumberType>
    := <TextureType>
    := <RWTextureType>

<ConstNumberElement> := <ConstNumber>
    := <ConstNumberElement> ',' <ConstNumber>

<ConstNumber> := IntNumber
    := FloatNumber
    := String
    := <NumberType> '(' [ <ConstNumberElement> ] ')'

<MateDateElement> := Id
    := Id '=' <ConstNumber>

<MateDateElementList> := <MateDateElement>
    := <MateDateElementList> ',' <MateDateElement>

<MateDate> := '[' [ <MateDateElementList> ] ']'
<NullableMateData> := <MateDate>
    := 

<DefineProperty> := <NullableMateData> <Type> Id
    := <NullableMateData> <Type> Id '=' <ConstNumber>
    := <NullableMateData> <Type> Id '[' IntNumber ']'

<DefinePropertyStatement> := <DefinePropertyStatement> <DefineProperty> ';'
    := <DefinePropertyStatement> <MateDate> '{' <DefinePropertyStatement> '}'
    := 

<ImportStatement> := 'import' String 'as' Id ';'

<PropertyStatement> := 'property' '{' <DefinePropertyStatement>  '}' ';'

<IncludeElement> := Id
    := <IncludeElement> '.' Id

<IncludeElementList> := <IncludeElement>
    := <IncludeElementList> ',' <IncludeElement>

<Include> := '[' <IncludeElementList> ']'
    := '[' ']'
    :=

<CodeStatement> := 'code' Id <Include> Code ';'
    := 'code' <Include> Id Code ';'

<Conflig> := '<' <MateDateElementList> '>'
    := '<' '>'

<InoutElementList> := <NumberType> Id
    := <InoutElementList> ',' <NumberType> Id

<Inout> := '(' <InoutElementList> ')'
    := '(' ')'

<SectionStatement> := 'section' Id <Include> <Inout> '->' <Inout> Code ';'
<DefineMarcoStatement> := 'define' Id String ';'
    := 'define' Id ';'

<MaterialStatementElement> := <MaterialStatementElement> <SectionStatement>
    := <MaterialStatementElement> <DefineMarcoStatement>
    := 

<MaterialStatement> := <NullableMateData> 'material' Id 'as' Id <Conflig> '{' <MaterialStatementElement> '}' ';'
    := <NullableMateData> 'using' 'material' <IncludeElement> 'as' Id <Conflig> ';'

$ := <Statement>

<StatemenetElement> :=  <StatemenetElement> <ImportStatement>
    := <StatemenetElement> <CodeStatement>
    := <StatemenetElement> <MaterialStatement> 
    := 

<Statement> := <StatemenetElement>
    := <PropertyStatement> <StatemenetElement>

